@echo off
SETLOCAL


REM Generate a set of protocols for demonstration and training and practice. 

REM This script allows one to select -upright or -supine with the first parameter.
set posture=%1
set pstr=%posture:~0,2%


REM This script takes an additional input parameter, which can be sml, med, lrg. It adds this as a modifier to the target file list
REM so that the targets can be tuned to different size subject. It also inserts this string into the script file name
set size=%2
set sz=%size:~0,1%

REM Alias to the compiler.
set COMPILER=DexSimulatorApp.exe
set SOURCE=..\DexSourceCode

REM Task Counter
REM Here I am adopting a strategy by which all the protocols of the same type but for different size subjects
REM  (small, medium and large) will use the same ID number for each task, but that the different protocols will 
REM  start numbering from different values, i.e. Dynamics = 200, Upright = 300, Supine = 400, Reduced = 500
set task=980
if /I %posture% EQU supine SET task=990 

REM Write a header into the file.
ECHO #DEX protocol file for demonstration of tasks.
ECHO #Generated by %0:  %date% %time%
ECHO #Format: "CMD_TASK", task id, task file, display name

REM ****************************************************************************

REM Perform the install of the equipment in the specified configuration (seated or supine)..
REM Each subject should do this, even when the configuration has not changed, to be sure that the CODAs are aligned.
if /I %posture% EQU supine GOTO :SUPINE 
call %SOURCE%\DexCreateInstallTask.bat Upright
GOTO :NEXT
:SUPINE
call %SOURCE%\DexCreateInstallTask.bat Supine
:NEXT

REM The force sensor offsets are also measured and suppressed at the start for each subject.
REM Construct a file tag.
call %SOURCE%\DexCreateOffsetTask.bat -deploy

REM ****************************************************************************
REM
REM Coefficient of Friction tests.
REM

REM Create the script filename.
set /A "task=task+1"
set filename=%tag%Fric.dex
DexSimulatorApp -friction -filter=2.0 -duration=15.0 -tag=%tag% -pinch=%force% -compile=%filename% 
echo CMD_TASK,%task%,%filename%,%task% Friction

REM ****************************************************************************

REM
REM Oscillations
REM

set /A "task=task+1"

REM All the oscillations are in the same direction and of the same duration.
set direction=Vertical
set dir=%direction:~0,4%
set duration=20.0

set mass=400gm
set frequency=1.00
set range=OscillationRangesNominalVertical.txt:%sz%

REM Construct a file tag.
set tag=%sz%%task%O%pstr%

REM Create a filename.
REM set filename=%tag%Osc%dir%%mass%.dex
set filename=%tag%.dex
%COMPILER% -oscillations -prep -nochecks -%mass% -%posture% -%direction% -range=%range% -frequency=%frequency% -duration=%duration% -tag=%tag% -compile=%filename% >> joe.txt
echo CMD_TASK,%task%,%filename%,%task% Oscillations

REM ***************************************************************************

REM
REM Targeted Movements
REM

set /A "task=task+1"

set mass=400gm
set direction=Vertical

REM Construct a file tag.
set tag=%sz%%task%T%pstr%

REM Put all the paramters together for the compiler.
set params=-targeted -prep -nochecks -%mass% -%posture% -%direction% -tag=%tag% -targets=TargetedTargets%direction%30.txt:6%sz%  

REM Generate a script filename based on the parameters.
set filename=%tag%0.dex

REM Generate the script to do the task.
%COMPILER% %params%  -compile=%filename% 

REM Record the task in the protocol definition, labelled appropriately.
echo CMD_TASK,%task%,%filename%,%task% Targeted

REM ****************************************************************************

REM
REM Discrete Movements
REM


set mass=400gm
set direction=Vertical
set range=DiscreteRanges%direction%.txt:%sz%

set /A "task=task+1"

set tag=%sz%%task%D%
set eyes=open  
set ey=%eyes:~0,2%
set filename=%tag%%ey%.dex
%COMPILER% -discrete -prep -nochecks -%mass% -%posture% -%direction% -%eyes% -range=%range% -delays=DiscreteDelaySequences20.txt:7 -tag=%tag% -compile=%filename%
echo CMD_TASK,%task%,%filename%,%task% Discrete Open

set /A "task=task+1"

set tag=%sz%%task%D%
set eyes=closed
set ey=%eyes:~0,2%
set filename=%tag%%ey%.dex
%COMPILER% -discrete -prep -nochecks -%mass% -%posture% -%direction% -%eyes% -range=%range% -delays=DiscreteDelaySequences20.txt:7 -tag=%tag% -compile=%filename%
echo CMD_TASK,%task%,%filename%,%task% Discrete Closed

REM ****************************************************************************
REM
REM Collisions
REM

set /A "task=task+1"

set sq=8
set mass=400gm

REM Construct a file tag.
set tag=%sz%%task%C

REM Put all the paramters together for the compiler.
set params=-collisions -prep -nochecks -%mass% -%posture% -tag=%tag% -delays=CollisionsSequences30.txt:%sq%

REM Generate a script filename based on the parameters.
REM set filename=%tag%Col%mass%%sq%.dex
set filename=%tag%%sq%.dex

REM Generate the script to do the task.
%COMPILER% %params%  -compile=%filename% 

REM Record the task in the protocol definition, labelled appropriately.
echo CMD_TASK,%task%,%filename%,%task% Collisions

REM ****************************************************************************

REM
REM Show that protocol is finished.
REM
set /A "task=task+1"
echo CMD_TASK,%task%,TaskFinishProtocol.dex,(Done - press Back)

ENDLOCAL
goto :EOF

@echo off
SETLOCAL


REM Generate the Dynamics Flight Reduced protocol. (From a copy of the DexGenerateDynamics.bat)

REM This script allows one to select -upright or -supine with the first parameter.
set posture=%1
set pstr=%posture:~0,2%

REM This script takes an additional input parameter, which can be sml, med, lrg. It adds this as a modifier to the target file list
REM so that the targets can be tuned to different size subject. It also inserts this string into the script file name
set size=%2
set sz=%size:~0,1%

REM Alias to the compiler.
set COMPILER=DexSimulatorApp.exe
set SOURCE=..\DexSourceCode

REM Task Counter
REM Here I am adopting a strategy by which all the protocols of the same type but for different size subjects
REM  (small, medium and large) will use the same ID number for each task, but that the different protocols will 
REM  start numbering from different values, i.e. Dynamics = 200, Upright = 300, Supine = 400, Reduced = 500, 
REM  and BDC Dynamics = 600, Upright = 700, Supine = 800 and Reduced = 900.
REM Commissioning Seated = 160
set task=150
if /I %posture% EQU supine SET task=180 
if /I %posture% EQU Supine SET task=180 

REM Write a header into the file.
ECHO #DEX protocol file for Commissioning protocol.
ECHO #Generated by %0:  %date% %time%
ECHO #Format: "CMD_TASK", task id, task file, display name

REM ****************************************************************************
REM
REM For common tasks we assume that the scripts have already been constructed, 
REM so we just create the appropriate lines in the procedure file.
REM
REM ****************************************************************************

REM Standard tasks at the start of a subsession.

REM Perform the install of the equipment.
if /I %posture% EQU supine GOTO :SUPINE 
if /I %posture% EQU Supine GOTO :SUPINE 
call %SOURCE%\DexCreateInstallTask.bat Upright
GOTO :NEXT
:SUPINE
call %SOURCE%\DexCreateInstallTask.bat Supine
:NEXT

REM Make sure that the audio is set loud enough.
call %SOURCE%\DexCreateAudioTask.bat 

REM The force sensor offsets are also measured and suppressed at the start for each subject.
call %SOURCE%\DexCreateOffsetTask.bat -%posture% -deploy -sit

REM ****************************************************************************

REM As part of commissioning we want to check that the masses can be removed
REM  and inserted into the cradles and that DEX correctly identifies each one.
REM We only do this in the upright posture.

if /I %posture% EQU supine GOTO :SKIPMASS 
if /I %posture% EQU Supine GOTO :SKIPMASS 

REM A static script exists already, so I don't bother creating a batch file to 
REM  create the task. I just write the line to the protocol file.
set /A "task=task+1"
echo CMD_TASK,%task%,TaskCheckMASS.dex,%task% Test Mass Selection

:SKIPMASS

REM ****************************************************************************

REM
REM Coefficient of Friction tests.
REM

REM Start the trial counter for the friction tests.
set fric_seq=0

call %SOURCE%\DexCreateFrictionTask.bat 2.5 -prep
call %SOURCE%\DexCreateFrictionTask.bat 1.0
call %SOURCE%\DexCreateFrictionTask.bat 0.5 -stow


REM ****************************************************************************

if /I %posture% EQU supine GOTO :SKIPTESTS 
if /I %posture% EQU Supine GOTO :SKIPTESTS 

REM
REM Check the linearity of the CODA tracking cameras.
REM

set duration=20.0
set frequency=0.25

set /A "task=task+1"
set tag=X%task%TRKR
set filename=%tag%.dex
%COMPILER% -tracker -prep -roll -frequency=%frequency% -duration=%duration% -tag=%tag% -compile=%filename% 
echo CMD_TASK,%task%,%filename%,%task% Test Tracker ROLL

set /A "task=task+1"
set tag=X%task%TRKP
set filename=%tag%.dex
%COMPILER% -tracker -pitch -done  -frequency=%frequency% -duration=%duration% -tag=%tag% -compile=%filename% 
echo CMD_TASK,%task%,%filename%,%task% Test Tracker PITCH

REM
REM Check the calibration of the inertial and force sensors.
REM

set duration=15.0
set frequency=1.0

set /A "task=task+1"
set tag=X%task%VSLW
set filename=%tag%.dex
%COMPILER% -sensors -prep -vertical -frequency=%frequency% -duration=%duration% -tag=%tag% -compile=%filename% 
echo CMD_TASK,%task%,%filename%,%task% Sensor Up-Down Slow

set /A "task=task+1"
set tag=X%task%DSLW
set filename=%tag%.dex
%COMPILER% -sensors -depth -frequency=%frequency% -duration=%duration% -tag=%tag% -compile=%filename% 
echo CMD_TASK,%task%,%filename%,%task% Sensor Front-Back Slow

set /A "task=task+1"
set tag=X%task%SSLW
set filename=%tag%.dex
%COMPILER% -sensors -sideways -frequency=%frequency% -duration=%duration% -tag=%tag% -compile=%filename% 
echo CMD_TASK,%task%,%filename%,%task% Sensor Left-Right Slow

set duration=10.0
set frequency=1.33

set /A "task=task+1"
set tag=X%task%VFST
set filename=%tag%.dex
%COMPILER% -sensors -vertical -frequency=%frequency% -duration=%duration% -tag=%tag% -compile=%filename% 
echo CMD_TASK,%task%,%filename%,%task% Sensor Up-Down Fast

set /A "task=task+1"
set tag=X%task%DFST
set filename=%tag%.dex
%COMPILER% -sensors -depth -frequency=%frequency% -duration=%duration% -tag=%tag% -compile=%filename% 
echo CMD_TASK,%task%,%filename%,%task% Sensor Front-Back Fast

set /A "task=task+1"
set tag=X%task%SFST
set filename=%tag%.dex
%COMPILER% -sensors -sideways -frequency=%frequency% -duration=%duration% -tag=%tag% -compile=%filename% 
echo CMD_TASK,%task%,%filename%,%task% Sensor Left-Right Fast



REM ****************************************************************************

REM Now we start generating the science task scripts specific to this protocol.
REM Here we build the scripts as we go and we create the entries in the protocol file.

REM ****************************************************************************

REM
REM Oscillations
REM

REM All the oscillations are in the same direction and of the same duration.
set direction=Vertical
set dir=%direction:~0,4%
set duration=30.0

REM Start the trial counter for the oscillations.
set osc_seq=100

set mass=600gm
set frequency=1.00
set prep=-prep
set range=OscillationRangesNominalVertical.txt:%sz%
call %SOURCE%\DexCreateOscillationTask.bat

set mass=600gm
set frequency=1.00
set prep=
set range=OscillationRangesNominalVertical.txt:%sz%
call %SOURCE%\DexCreateOscillationTask.bat


REM ****************************************************************************

REM
REM Targeted Movements
REM

set mass=800gm
set nblocks=2
set movements=20

REM
REM Vertical Direction
REM
set direction=Vertical
call %SOURCE%\DexCreateTargetedTasks.bat

:SKIPTESTS

REM ****************************************************************************


REM
REM Collisions
REM

set mass=600gm
set direction=Vertical
set targets=CollisionSequences20.txt
set nblocks=2
call %SOURCE%\DexCreateCollisionTasks.bat


REM ****************************************************************************

REM
REM Discrete Movements
REM

REM All the trials mass.
set mass=400gm
set delays=DiscreteDelaySequences20.txt

REM Start the trial counter for the oscillations.
set dsc_seq=0

set direction=Vertical
set eyes=open  
set range=DiscreteRangesVertical.txt:%sz%
call %SOURCE%\DexCreateDiscreteTask.bat -prep

set direction=Vertical
set eyes=closed
set range=DiscreteRangesVertical.txt:%sz%
call %SOURCE%\DexCreateDiscreteTask.bat

set direction=Horizontal
set eyes=open  
set range=DiscreteRangesHorizontal.txt:%sz%
call %SOURCE%\DexCreateDiscreteTask.bat -prep

set direction=Horizontal
set eyes=closed
set range=DiscreteRangesHorizontal.txt:%sz%
call %SOURCE%\DexCreateDiscreteTask.bat


REM ****************************************************************************

REM
REM Show that protocol is finished.
REM
set /A "task=task+1"
echo CMD_TASK,%task%,TaskFinishProtocol.dex,(Done: press Back)

ENDLOCAL
goto :EOF

